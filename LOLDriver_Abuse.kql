//Author: Ala Dabat
//Basic Hunt
let VulnerableDriverData = externaldata (
    Id:string, Author:string, Created:string, Command:string, Description:string, Usecase:string, Category:string, Privileges:string, MitreID:string, OperatingSystem:string, Resources:string, DriverInfo:string, ContactPerson:string, HandleReference:string, DetectionMethod:string, MD5_Hashes:string, SHA1_Hashes:string, SHA256_Hashes:string, PublisherInfo:string, CompanyInfo:string, VulnerabilityDetails:string, MD5_Authentihash:string, SHA256_Authentihash:string, SHA1_Authentihash:string, VerificationStatus:string, TagsInfo:string
) ["https://www.loldrivers.io/api/drivers.csv"] with (format="csv", ignoreFirstRecord=true);

let VulnerableDriversSHA256 = VulnerableDriverData
| extend IndividualSHA256 = split(SHA256_Hashes, ', ')
| mv-expand IndividualSHA256
| where isnotempty(IndividualSHA256)
| extend NormalizedSHA256 = trim(" ", tolower(IndividualSHA256));

let LoadedDrivers = DeviceEvents
| where ActionType has "DriverLoad"
| extend NormalizedDeviceSHA256 = trim(" ", tolower(SHA256));

LoadedDrivers
| join kind=inner VulnerableDriversSHA256 on $left.NormalizedDeviceSHA256 == $right.NormalizedSHA256
| where not(FileName == "DBUtilDrv2.sys" and Category != "Vulnerable Driver")
| summarize arg_max(Timestamp, *) by DeviceName, NormalizedDeviceSHA256
| extend VT_Domain = iff(isnotempty(NormalizedDeviceSHA256), strcat("https://www.virustotal.com/gui/file/", NormalizedDeviceSHA256), NormalizedDeviceSHA256)
| project-away ContactPerson, HandleReference;
